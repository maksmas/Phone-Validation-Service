buildscript {
	ext {
		springBootVersion = '2.0.4.RELEASE'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

group = 'mmm.neotech'
version = '1.0.0-RELEASE'
sourceCompatibility = 1.8

repositories {
	mavenCentral()
}

sourceSets {
    integrationTest {

        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file("src/integrationTest/java")
        }
        resources.srcDir file("src/integrationTest/resources")
    }
}

build {
    reporting {
        enabled = true
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

task integrationTest(type: Test) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
}

check.dependsOn integrationTest
integrationTest.mustRunAfter test

dependencies {
	compile('org.springframework.boot:spring-boot-starter-web:2.0.2.RELEASE')
	compile('org.jsoup:jsoup:1.11.3')
	testCompile('org.springframework.boot:spring-boot-starter-test:2.0.2.RELEASE')
}
